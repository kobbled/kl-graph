PROGRAM class_name
%NOBUSYLAMP
%COMMENT = 'graph class'
%NOLOCKGROUP
%NOPAUSE = COMMAND + TPENABLE + ERROR

%defeval BASE_CLASSNAME class_name

%include systemlib.klt
path_default_types

%include errors.klt

--define graph types
%include graph.klt
--user graph type
graph_type_define(class_name)


--for A* search
%ifdef INCLUDE_A_STAR
--priority queue types
queue_type_define(FRONTIER_NAME)
--linear hashes
hash_header_define(PATH_TRACK_NAME)
hash_type_define(PATH_TRACK_NAME)
%include define_type.m
t_hash(hashname,hval_def,PATH_TRACK_NAME)
%endif

%defeval class_name BASE_CLASSNAME

VAR
  this : PATH pathheader = hdr_graph, nodedata = GRAPH_TYPE
  visited : PATH nodedata = t_BOOL
%ifdef INCLUDE_A_STAR
  track : PATH pathheader = t_linear_hsh, nodedata = hashname
%endif

%include errors.klh
%from strings.klh %import i_to_s

--abstract data structures
%ifdef INCLUDE_A_STAR
--priority queue
define_priority_queue(FRONTIER_NAME,CONFIG_FILENAME)
--tracking dict
define_tracking_dict(PATH_TRACK_NAME,CONFIG_FILENAME)
%endif

%defeval class_name BASE_CLASSNAME
-- graph class members
%include graph.klh
%include graph.private.klh

--private

ROUTINE clear_graph
  VAR
    i : INTEGER
    status : INTEGER
  BEGIN
    i = PATH_LEN(this)
    WHILE PATH_LEN(this) > 0 DO
		  DELETE_NODE(this,i,status)
      CHK_STAT(status)
      i = i - 1
	  ENDWHILE
  END clear_graph

ROUTINE clear_visited
  VAR
    i : INTEGER
    status : INTEGER
  BEGIN
    i = PATH_LEN(visited)
    WHILE PATH_LEN(visited) > 0 DO
		  DELETE_NODE(visited,i,status)
      CHK_STAT(status)
      i = i - 1
	  ENDWHILE
  END clear_visited

ROUTINE reset_visited
  VAR
    i : INTEGER
  BEGIN
    FOR i=1 TO PATH_LEN(visited) DO
      visited[i].v = FALSE
    ENDFOR
  END reset_visited

ROUTINE append_adj
  VAR
    i : INTEGER
    adj : GRAPH_ADJ_TYPE
  BEGIN
    FOR i=1 TO ARRAY_LEN(this[id].adj) DO
      adj = this[id].adj[i]
      IF (UNINIT(adj.GRAPH_NODE_NAME)) THEN
        this[id].adj[i] = data
        RETURN ;
      ENDIF
    ENDFOR

    -- no free slot in adjacency list
    karelError(INVALID_INDEX,'class_name node ' + i_to_s(id) + ' adjacency list is full.', ER_ABORT)

  END append_adj

ROUTINE DFSUtil
  VAR
    i : INTEGER
    status : INTEGER
    adj : GRAPH_ADJ_TYPE
  BEGIN
    vistd[nde].v = TRUE
    --add to path
    APPEND_NODE(out_pth, status)
    out_pth[PATH_LEN(out_pth)].v = nde

    i = 1
    adj = grph[nde].adj[i]
    WHILE (NOT UNINIT(adj.GRAPH_NODE_NAME)) DO
      IF (vistd[adj.GRAPH_NODE_NAME].v = FALSE) THEN
        DFSUtil((adj.GRAPH_NODE_NAME), grph, vistd, out_pth)
      ENDIF

      i = i + 1
      IF i > ARRAY_LEN(grph[nde].adj) THEN
        RETURN
      ENDIF

      adj = grph[nde].adj[i]
    ENDWHILE
  END DFSUtil

%ifdef INCLUDE_A_STAR
ROUTINE clear_track
  VAR
    i : INTEGER
    status : INTEGER
  BEGIN
    i = PATH_LEN(track)
    WHILE PATH_LEN(track) > 0 DO
		  DELETE_NODE(track,i,status)
      CHK_STAT(status)
      i = i - 1
	  ENDWHILE
  END clear_track

ROUTINE reconstruct_path
  VAR
    current : INTEGER
    status : INTEGER
    nde : hval_type
    b : BOOLEAN
  BEGIN
    current = goal
    APPEND_NODE(pth, status)

    REPEAT
      INSERT_NODE(pth, 1, status)
      pth[1].v = current

      --get previous node
      b = `PATH_TRACK_NAME`__get(i_to_s((current)), nde, track)
      current = nde.prev_node

    UNTIL(current = 0)

  END reconstruct_path
%endif

-- public

ROUTINE new
  VAR
    i : INTEGER
    status : INTEGER
  BEGIN
    clear_graph
    clear_visited

    FOR i=1 TO no_of_verts DO
      append
    ENDFOR

    --add header information
    this.directional = directional
  END new

ROUTINE destructor
  BEGIN
    clear_graph
    clear_visited
%ifdef INCLUDE_A_STAR
    clear_track
%endif
  END destructor

-- **** MUST BE DEFINED IN TEMPLATE FILE (.klt)
define_new_node(class_name)
define_add_edge(class_name)

ROUTINE append
  VAR
    status : INTEGER
  BEGIN
    APPEND_NODE(this,status)
    CHK_STAT(status)
    --add visited node
    APPEND_NODE(visited,status)
    visited[PATH_LEN(visited)].v = FALSE
    CHK_STAT(status)
  END append

ROUTINE is_neighbor
  VAR
    i : INTEGER
    adj : GRAPH_ADJ_TYPE
  BEGIN
    i = 1
    FOR i=1 TO ARRAY_LEN(this[nde].adj) DO
      adj = this[nde].adj[i]
      IF (UNINIT(adj.GRAPH_NODE_NAME)) THEN
        RETURN(FALSE)
      ENDIF

      IF adj.GRAPH_NODE_NAME = neighbor THEN
        RETURN(TRUE)
      ENDIF
    ENDFOR

    RETURN(FALSE)
  END is_neighbor

ROUTINE list_neighbors
  VAR
    neighbors : ARRAY[MAX_NEIGHBORS] OF INTEGER
    i : INTEGER
    adj : GRAPH_ADJ_TYPE
  BEGIN
    i = 1
    adj = this[nde].adj[i]
    WHILE (NOT UNINIT(adj.GRAPH_NODE_NAME)) DO
      neighbors[i] = adj.GRAPH_NODE_NAME

      i = i + 1
      IF i > ARRAY_LEN(this[nde].adj) THEN
        RETURN(neighbors)
      ENDIF

      adj = this[nde].adj[i]
    ENDWHILE

    RETURN(neighbors)
  END list_neighbors

ROUTINE DFS
  VAR
    i : INTEGER
  BEGIN
    reset_visited

    IF start_node > 0 THEN
      IF start_node > PATH_LEN(this) THEN
        karelError(INVALID_INDEX,'node ' + i_to_s(start_node) + ' is not in graph.', ER_ABORT)
      ENDIF

      IF (NOT visited[start_node].v) THEN
        DFSUtil(start_node, this, visited, pth)
      ENDIF
      
    ELSE

      FOR i=1 TO PATH_LEN(this) DO
        IF (NOT visited[i].v) THEN
          DFSUtil(i, this, visited, pth)
        ENDIF
      ENDFOR
    
    ENDIF

    -- return(out_pth)

  END DFS

ROUTINE BFS
  VAR
    i : INTEGER
    v : INTEGER
    adj : GRAPH_ADJ_TYPE
    status : INTEGER
  BEGIN
    IF UNINIT(start_node) OR (start_node = 0) THEN start_node = 1 ; ENDIF 

    reset_visited
    --clear queue
    i = PATH_LEN(queue)
    WHILE PATH_LEN(queue) > 0 DO
		  DELETE_NODE(queue,i,status)
      CHK_STAT(status)
      i = i - 1
	  ENDWHILE


    visited[start_node].v = TRUE
    --push first node onto queue
    APPEND_NODE(queue,status)
    queue[PATH_LEN(queue)].v = start_node

    REPEAT
      --next node from queue
      v = queue[1].v
      DELETE_NODE(queue,1,status)

      --add to path list
      APPEND_NODE(pth, status)
      pth[PATH_LEN(pth)].v = v

      --if reach goal abort
      IF (v=goal) THEN
        RETURN
      ENDIF

      --get neighbors of the popped node
      i = 1
      adj = this[v].adj[i]
      REPEAT
        --if adjacent node has not been visited 
        --add to queue
        IF (NOT visited[ adj.GRAPH_NODE_NAME ].v) THEN
          visited[ adj.GRAPH_NODE_NAME ].v = TRUE
          --push node on queue
          APPEND_NODE(queue,status)
          queue[PATH_LEN(queue)].v = (adj.GRAPH_NODE_NAME)
        ENDIF

        i = i + 1
        IF i > ARRAY_LEN(this[v].adj) THEN
          karelError(INVALID_INDEX,'increase MAX-NEIGHBORS in class_name', ER_ABORT)
        ENDIF
        adj = this[v].adj[i]
      --repeat until adjacency list is null
      UNTIL(UNINIT(adj.GRAPH_NODE_NAME) )

    -- repeat until queue is empty
    UNTIL(PATH_LEN(queue) = 0)

  END BFS

%ifdef INCLUDE_A_STAR
ROUTINE dijkstra_search
  VAR
    update_track : hval_type
    current : INTEGER
    i : INTEGER
    adj : GRAPH_ADJ_TYPE
    new_cost : GRAPH_EDGE_VTYPE
    b : BOOLEAN
  BEGIN
    --initialize frontier
    `FRONTIER_NAME`__init
    --put start node on frontier
    `FRONTIER_NAME`__push(start_node, 0)
    --initialize tracking dictionary
    `PATH_TRACK_NAME`__init(track)
    -- add start node to dict
    update_track.prev_node = 0
    update_track.cost_so_far = 0
    b = `PATH_TRACK_NAME`__put(i_to_s(start_node), update_track, track)
    IF NOT b THEN karelError(INVALID_INDEX,'error writing to track dict', ER_ABORT) ; ENDIF

    REPEAT
      --get next node from priority queue
      current = `FRONTIER_NAME`__pop_min

      IF current = goal THEN
        --reconstruct path from tracking list
        reconstruct_path(start_node, goal, pth)
        RETURN
      ENDIF

      --loop through adjacency list to see if node is untracked
      --or has a lower cost than the previous path with this
      --frontier node
      i = 1
      adj = this[current].adj[i]
      REPEAT

        --calculate cost of path from start_node->current->adj
        b = `PATH_TRACK_NAME`__get(i_to_s(current), update_track, track)
        IF NOT b THEN karelError(INVALID_INDEX,'error reading from track dict', ER_ABORT) ; ENDIF
        new_cost = update_track.cost_so_far + adj.GRAPH_EDGE_NAME

        --get cost so far of frontier node
        b = `PATH_TRACK_NAME`__get(i_to_s((adj.GRAPH_NODE_NAME)), update_track, track)
        IF (NOT b) OR (new_cost < update_track.cost_so_far) THEN
          --update cost and previous node in tracking dictionary
          update_track.prev_node = current
          update_track.cost_so_far = new_cost
          b = `PATH_TRACK_NAME`__put(i_to_s((adj.GRAPH_NODE_NAME)), update_track, track)
          IF NOT b THEN karelError(INVALID_INDEX,'error writing to track dict', ER_ABORT) ; ENDIF

          --put node and cost on frontier
          `FRONTIER_NAME`__push((adj.GRAPH_NODE_NAME), (new_cost))

        ENDIF

        i = i + 1
        IF i > ARRAY_LEN(this[current].adj) THEN
          karelError(INVALID_INDEX,'increase MAX-NEIGHBORS in class_name', ER_ABORT)
        ENDIF
        adj = this[current].adj[i]
        --repeat until adjacency list is null
      UNTIL(UNINIT(adj.GRAPH_NODE_NAME) )

    UNTIL(`FRONTIER_NAME`__empty)

    reconstruct_path(start_node, current, pth)
    karelError(INVALID_INDEX,'class_name did not find goal', ER_WARN)

  END dijkstra_search

%endif

BEGIN
END class_name
